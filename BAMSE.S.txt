    0 ERR=0:ERT=6600
   10 TEXT 
   30 MODE=0:PX=0:PY=0:RD=0:C=0
   40 V=0:H=0:PS=0:PSM=0:PI=0:PIM=0:PD=0:PDM=0:PB=0:PBM=0:EXP=0
   45 GOLD=0:BANK=0:PA=0:PW=0:BSS=0:BSI=0:BSD=0:BSB=0:BSE=0
   50 DIM MI(10): FOR I=1 TO 10:MI(I)=0: NEXT I
   51 NR=0:DF=0:LVL=0
   52 REM  DO NOT CHANGE ORDER OF VARS ABOVE THIS POINT
   53 DIM A$(15)
   55 DIM NOM$(56):NOM$="SLIME   GHOST   TROLL   WORM    DRAGON  STALKER VAMPIRE "
   60 DIM MAP(800)
   70 NMN=6: DIM MS(6),MD(6),MB(6),MA(6),MW(6),MC(6),MF(6),MT(6),MV(6)
   75 DIM NOML(6):NOML(0)=5:NOML(1)=5:NOML(2)=5:NOML(3)=4:NOML(4)=6:NOML(5)=7:NOML(6)=7
   80 REM  MONSTER DEFINITIONS, SPEED,DEXT,BODY,ARMOR,WEAPON,COLOR,FREQ,TREAS%,TREA VALUE
   90 MS(0)=50:MD(0)=50:MB(0)=100:MA(0)=0:MW(0)=0:MC(0)=12:MF(0)=25:MT(0)=50:MV(0)=50
  100 MS(1)=100:MD(1)=100:MB(1)=50:MA(1)=1:MW(1)=0:MC(1)=15:MF(1)=30:MT(1)=85:MV(1)=100
  110 DIM MFT(4):MFT(0)=5:MFT(1)=20:MFT(2)=50:MFT(3)=20:MFT(4)=5
  120 MS(2)=150:MD(2)=150:MB(2)=50:MA(2)=2:MW(2)=1:MC(2)=9:MF(2)=35:MT(2)=75:MV(2)=200
  130 MS(3)=200:MD(3)=50:MB(3)=200:MA(3)=3:MW(3)=2:MC(3)=3:MF(3)=8:MT(3)=75:MV(3)=300
  140 MS(4)=200:MD(4)=100:MB(4)=50:MA(4)=4:MW(4)=3:MC(4)=1:MF(4)=2:MT(4)=100:MV(4)=400
  142 MS(5)=75:MD(5)=400:MB(5)=50:MA(5)=3:MW(5)=2:MC(5)=5:MF(5)=2:MT(5)=25:MV(5)=250
  144 MS(6)=75:MD(6)=150:MB(6)=75:MA(6)=2:MW(6)=3:MC(6)=14:MF(6)=2:MT(6)=75:MV(6)=350
  145 LVL=0
  150 I=1: POKE -16304,0: POKE -16301,0: POKE -16300,0: POKE -16297,0
  160 POKE 34,20: POKE 35,24
  220 CALL -936
  221 PRINT : INPUT "DO YOU WANT SOUND EFFECTS?",A$: IF LEN(A$)=0 THEN 221: IF A$(1,1)="Y" THEN 222: POKE 4096,96: POKE 4352,96
  222 CALL -936: PRINT : INPUT "RESTORE PREVIOUS GAME?",A$
  224 IF LEN(A$)=0 THEN 230: IF A$(1,1)="N" THEN 230
  226 CALL -936: PRINT : INPUT "ENTER GAME NUMBER (0-15)",GN: IF GN<0 OR GN>15 THEN 226
  228 POKE 0,GN:ERT=6600: CALL 994: GOTO 390
  230 CALL -936: PRINT : INPUT "STANDARD GAME?",A$: IF LEN(A$)=0 THEN 230: IF A$(1,1)="N" THEN 231:NR=5:MODE=0:DF=5:V=10:H=20: GOTO 320
  231 CALL -936: PRINT : PRINT "HOW MANY ROOMS PER LEVEL (2-10)": INPUT "(I SUGGEST 5)",NR: IF NR<2 OR NR>10 THEN 231
  240 V=20:H=4*NR: IF H<10 THEN H=10: IF H>40 THEN H=40
  245 MODE=1: IF H>20 THEN 270
  250 CALL -936: PRINT : INPUT "HIRES OR TEXT GRAPHICS (H OR T) ?",A$:MODE=0
  260 IF A$(1,1)="T" THEN MODE=1
  265 IF MODE=0 THEN V=10
  270 CALL -936: PRINT "1=PUSHOVER 5=AVERAGE   8=TOUCH AND GO"
  280 PRINT "2=TOO EASY 6=CHALLENGE 9=VERY DANGEROUS"
  290 PRINT "3=BEGINNER 7=TRICKY   10=YOU'RE NUTS!!"
  300 INPUT "4=SAFE TRIP >>> HOW DIFFICULT >>>",DF
  310 IF DF<1 OR DF>10 THEN 270
  312 GOTO 320
  320 EXP=0:PSM=20:PS=20:PIM=20:PI=20:PDM=20:PD=20:PBM=20:PB=20:PA=0:PW=0
  330 BSS=20:BSI=20:BSD=20:BSB=20:BSE=0
  340 DIM RL(20),RW(20),RH(20)
  350 DIM RF(20)
  360 DIM SDL(20)
  370 DIM MNL(20),MNT(20),MNA(20),MNS(20),MND(20),MNB(20)
  380 DIM CHL(20),CHS(20),CHC(20)
  390 CALL -936: PRINT : PRINT "CONSTRUCTING A NEW DUNGEON LEVEL...": GOSUB 2510: GOSUB 2510
  400 FOR I=0 TO H*V:MAP(I)=0: NEXT I
  410 PF=EXP/40+PSM/4+PIM/4+PDM/4+PBM/4
  415 IF PF>30000 THEN PF=30000
  420 IF PF<20 THEN PF=20
  430 CALL -936: PRINT : PRINT "NOW BUILDING: ROOMS...": GOSUB 2510
  440 FOR I=1 TO NR
  450 RL(I)= RND (H*V): IF MAP(RL(I))#0 THEN 450
  460 RW(I)= RND (5)+2:RH(I)= RND (3)+2
  470 X=RL(I) MOD H
  480 Y=RL(I)/H
  490 IF X+RW(I)>=H THEN 450
  500 IF Y+RH(I)>=V THEN 450
  510 J=X
  520 K=Y
  530 IF MAP(K*H+J)#0 THEN 450
  540 K=K+1: IF K<=Y+RH(I) THEN 530:J=J+1: IF J<=X+RW(I) THEN 520
  550 FOR J=X TO X+RW(I)
  560 FOR K=Y TO Y+RH(I)
  570 MAP(K*H+J)=5
  580 NEXT K,J
  590 NEXT I
  600 CALL -936: PRINT : PRINT "...CORRIDORS...";: GOSUB 2510
  610 FOR I=1 TO NR:RF(I)=0: NEXT I
  620 FOR I=1 TO NR-1
  630 PRINT I;" ";
  640 SX=RL(I) MOD H
  650 SY=RL(I)/H
  660 WW=RW(I)+1:HH=RH(I)+1
  670 EX=RL(I+1) MOD H
  680 EY=RL(I+1)/H
  690 IF EX-SX#WW OR EY-SY#HH THEN 700:EX=EX+1:EY=EY+1
  700 IF RND (2) THEN 730
  710 DX= SGN (EX-SX):DY=0
  720 GOTO 740
  730 DY= SGN (EY-SY):DX=0
  740 IF SX=EX AND DX#0 THEN 920
  750 IF SY=EY AND DY#0 THEN 920
  760 IF DX=0 AND DY=0 THEN 920
  770 SX=SX+DX:SY=SY+DY
  780 IF MAP(SX+SY*H)=5 THEN 740
  790 MAP(SX+SY*H)=10: IF MAP(SX-DX+(SY-DY)*H)=5 THEN MAP(SX+SY*H)=8
  800 J=1
  810 IF J=0 THEN 900
  820 X=SX+J:Y=SY
  830 IF J=-DX THEN 860
  840 IF X<0 OR X>H THEN 860
  850 IF MAP(X+Y*H)=5 THEN 970
  860 Y=SY+J:X=SX
  870 IF J=-DY THEN 900
  880 IF Y<0 OR Y>V THEN 900
  890 IF MAP(X+Y*H)=5 THEN 970
  900 J=J-1: IF J>=-1 THEN 810
  910 GOTO 740
  920 IF SX=EX AND SY=EY THEN 1080
  930 DX=0:DY=0
  940 IF SX=EX THEN DY= SGN (EY-SY)
  950 IF SY=EY THEN DX= SGN (EX-SX)
  960 GOTO 740
  970 MAP(SX+SY*H)=8
  980 J=1
  990 SX=RL(J) MOD H
 1000 SY=RL(J)/H
 1010 WW=RW(J)+1:HH=RH(J)+1
 1020 IF X>=SX AND X<=SX+RW(J) AND Y>=SY AND Y<=SY+RH(J) THEN 1050
 1030 J=J+1: IF J<=NR THEN 990
 1040 PRINT "HUH?": GOTO 1040
 1050 IF J=I+1 THEN 1080
 1060 RF(J)=1
 1070 GOTO 670
 1080 NEXT I
 1090 NSD=0:PL=-1
 1100 CALL -936: PRINT : PRINT "...DOORS...": GOSUB 2510
 1110 FOR I=0 TO H*V
 1120 IF MAP(I)#10 THEN 1150
 1130 IF PL>=0 THEN 1150
 1140 PL=I:PSL=I
 1150 IF MAP(I)=5 THEN MAP(I)=10
 1160 IF MAP(I)#8 THEN 1220
 1170 IF RND (4) THEN 1190
 1180 MAP(I)=10: GOTO 1220
 1190 IF RND (2) THEN 1220
 1200 IF NSD=NR THEN 1220
 1210 NSD=NSD+1:SDL(NSD)=I:MAP(I)=0
 1220 NEXT I: IF PL>=0 THEN 1230:PL=RL(1)+RW(1)+RH(1)*H:PSL=PL
 1230 FOR I=0 TO 4:MF(I)=0: NEXT I
 1240 PRT=(PF>35)+(PF>60)+(PF>120)+(PF>200)+(PF>300):J=PRT-2
 1250 MFTTL=0:K=0
 1260 FOR I=J TO 4: IF I<0 THEN 1290
 1270 IF K<5 THEN MF(I)=MFT(K)
 1280 MFTTL=MFTTL+MF(I)
 1290 K=K+1: NEXT I
 1300 CALL -936: PRINT : PRINT "...MONSTERS AND TREASURE...": GOSUB 2510
 1310 SUM=0
 1320 FOR I=1 TO NR
 1330 MNL(I)=-1:CHL(I)=-1
 1340 IF RND (3)=0 THEN 1510
 1350 MNL(I)=RL(I)
 1360 J=-1:K=0
 1370 P= RND (MFTTL)
 1380 FOR L=0 TO 4:K=K+MF(L): IF J>=0 THEN 1400
 1390 IF P<K THEN J=L
 1400 NEXT L: IF J<0 THEN J=4
 1405 IF J#4 THEN 1410: IF RND (100)>33 THEN 1410:J=5: IF RND (100)>50 THEN J=6
 1410 MAP(MNL(I))=MC(J)
 1420 MNT(I)=J
 1430 MNA(I)=0
 1440 MNS(I)=(PF/10)*(MS(J)/50)*DF
 1450 MND(I)=(PF/10)*(MD(J)/50)*DF
 1460 MNB(I)=(PF/10)*(MB(J)/50)*DF
 1470 SUM=SUM+MNB(I)
 1480 IF RND (100)>MT(J) THEN 1620
 1490 VAL=MV(J)
 1500 GOTO 1550
 1510 IF RND (5) THEN 1620
 1520 VAL=0
 1530 VAL=VAL+ RND (100)
 1540 IF RND (2) THEN 1530
 1550 VAL=(VAL*(5+ RND (11)))/10
 1560 VAL=(VAL*10)/NR
 1570 CHL(I)=RL(I)+RW(I)/2+(RH(I)/2)*H
 1580 MAP(CHL(I))=13
 1590 CHS(I)= RND (5)
 1600 CHC(I)=VAL
 1610 SUM=SUM+VAL
 1620 NEXT I
 1630 CALL -936: PRINT : PRINT "...MAGIC ITEMS": GOSUB 2510
 1640 J=0: FOR I=1 TO NR: IF CHL(I)<0 THEN 1700: IF J#0 THEN 1700: IF CHC(I)<0 THEN 1700
 1650 J=1
 1660 IF MNL(I)<0 OR MNT(I)<4 THEN 1670:K=99: IF MI(9)#0 AND MI(10)#0 THEN 1666
 1664 K=9: IF RND (100)<50 THEN K=10: IF MI(K)#0 THEN 1664
 1666 IF MNT(I)#4 AND K=99 THEN 1670:SUM=SUM-CHC(I):CHC(I)=-K: GOTO 1700
 1670 L=0: FOR K=1 TO 8: IF MI(K)#0 THEN 1680:L=K
 1680 NEXT K: IF L=0 THEN 1700
 1690 L= RND (8)+1: IF MI(L)#0 THEN 1690:SUM=SUM-CHC(I):CHC(I)=-L
 1700 NEXT I
 1720 CALL -936
 1730 PRINT "YOU ARE NOW A ";: GOTO 1740+PRT*10
 1740 PRINT "WARRIOR": GOTO 1810
 1750 PRINT "HERO": GOTO 1810
 1760 PRINT "SWASHBUCKLER": GOTO 1810
 1770 PRINT "CHAMPION": GOTO 1810
 1780 PRINT "SUPERHERO": GOTO 1810
 1790 PRINT "LORD": GOTO 1810
 1800 PRINT "CHEATER": GOTO 1810
 1810 LVL=LVL+1: PRINT "EXPERIENCE ON LEVEL ";LVL;" = ";SUM
 1930 INPUT "PRESS RETURN TO BEGIN",A$
 1940 CALL -936
 2000 DIM COM$(30),RTN(30)
 2010 DIM AC(3),WC(2)
 2020 COM$="ABEHIKLNORSTWXZ123456789D0?"
 2030 RTN(1)=4600:RTN(2)=4430:RTN(3)=4180:RTN(4)=5970:RTN(5)=5050:RTN(6)=4430:RTN(7)=4870:RTN(8)=4180
 2040 RTN(9)=5150:RTN(10)=5710:RTN(11)=4180:RTN(12)=6020:RTN(13)=4180:RTN(14)=6120:RTN(15)=6170
 2050 FOR I=16 TO 24:RTN(I)=5860: NEXT I
 2060 RTN(25)=6190:RTN(26)=5860
 2065 RTN(27)=6300
 2070 CALL 2048
 2071 SC=0:DEAD=0
 2080 CL=0:UPN=1:UPM=1: GOTO 3410
 2090 POKE 35,23: POKE 34,20: VTAB 24
 2100 TAB 1: CALL -868
 2110 PRINT "S";: IF PS=PSM THEN POKE 50,63: PRINT PS;: POKE 50,255
 2111 PRINT " I";: IF PI=PIM THEN POKE 50,63: PRINT PI;: POKE 50,255
 2112 PRINT " D";: IF PD=PDM THEN POKE 50,63: PRINT PD;: POKE 50,255
 2113 PRINT " B";: IF PB=PBM THEN POKE 50,63: PRINT PB;: POKE 50,255
 2114 PRINT " ";GOLD;"GP ";EXP;"EP";
 2120 TAB 1: VTAB 23
 2130 UPN=0: RETURN 
 2140 IF X>=0 AND X<=EXP/10 THEN 2160
 2150 INPUT "INVALID. TRY AGAIN",X: GOTO 2140
 2160 EXP=EXP-10*X: RETURN 
 2170 RD=1: IF MI(10)#0 THEN RD=40
 2180 PX=PL MOD H:PY=PL/H: CALL 2057
 2190 C=2: CALL 2051
 2200 UPM=0: RETURN 
 2210 J=0:DX=0:DY=0
 2220 XX=X+1:YY=Y: GOSUB 2350
 2230 XX=X-1: GOSUB 2350
 2240 XX=X:YY=Y+1: GOSUB 2350
 2250 YY=Y-1: GOSUB 2350
 2255 IF J=0 THEN RETURN 
 2258 IF J>1 THEN 2270: IF MAP((X+DX)+(Y+DY)*H)#5 THEN RETURN 
 2270 INPUT "WHICH DIRECTION?",A$
 2280 XX=X:YY=Y:J=0
 2290 IF A$="N" THEN YY=Y-1
 2300 IF A$="S" THEN YY=Y+1
 2310 IF A$="E" THEN XX=X+1
 2320 IF A$="W" THEN XX=X-1
 2330 GOSUB 2350
 2340 RETURN 
 2350 IF XX<0 OR XX>=H OR YY<0 OR YY>=V THEN RETURN 
 2360 IF CLR=255 THEN 2400
 2370 IF MAP(XX+YY*H)#CLR THEN RETURN 
 2380 J=J+1:DX=XX-X:DY=YY-Y
 2390 RETURN 
 2400 FOR KK=0 TO NMN: IF MAP(XX+YY*H)=MC(KK) THEN 2380: NEXT KK: RETURN 
 2410 PRINT "YOU TAKE ";HITS;" HITS."
 2420 UPN=1
 2430 PB=PB-HITS: IF PB>0 THEN RETURN 
 2435 PB=0: GOSUB 2090
 2440 FOR J=1 TO 3: GOSUB 2510: NEXT J: PRINT : PRINT "YOU'RE DEAD!  TOO BAD": PRINT "REINCARNATING TO LAST BRAIN SCAN";
 2450 FOR J=1 TO 5: GOSUB 2510: NEXT J: PRINT 
 2460 PS=BSS:PSM=BSS:PI=BSI:PIM=BSI:PD=BSD:PDM=BSD:PB=BSB:PBM=BSB:EXP=BSE
 2470 EXP=EXP+GOLD
 2480 BSS=BSS-(BSS/10):BSI=BSI-(BSI/10):BSD=BSD-(BSD/10):BSB=BSB-(BSB/10):BSE=BSE-(BSE/10)
 2490 CALL -936:UPN=1:C=MAP(PL):PX=PL MOD H:PY=PL/H: CALL 2051
 2500 UPM=1:PL=PSL:G=GOLD: GOSUB 5730:DEAD=1: RETURN 
 2510 FOR I=1 TO 3500: NEXT I: RETURN 
 2520 J=0:PROB=(((D*10)/PF)*10)/(A+1)
 2525 IF D>3000 THEN PROB=((((D/100)*10)/(PF/100))*10)/(A+1)
 2526 IF PROB<0 THEN PROB=5
 2530 IF W>=3 THEN PROB=PROB*2
 2535 IF PROB>100 THEN PROB=100
 2540 PRINT PROB;"% ";
 2550 IF RND (100)<PROB THEN J=1
 2560 RETURN 
 2570 IF DEAD=1 THEN 2610:CL=CL+1:OUT=0
 2580 IF RND (100)=0 THEN GOSUB 2650
 2585 DIST=1: GOSUB 2900
 2590 IF ALR THEN GOSUB 2895
 2600 IF MAF OR MRA>CL THEN GOSUB 3060
 2610 IF UPN THEN GOSUB 2090
 2620 IF UPM THEN GOSUB 2170
 2630 RETURN 
 2650 M=0
 2660 FOR I=1 TO NR: IF MNL(I)<0 THEN M=I: NEXT I
 2670 IF M=0 THEN RETURN 
 2680 CLR=10:X=PL MOD H:Y=PL/H:J=0
 2690 XX=X+1:YY=Y: GOSUB 2350
 2700 XX=X-1: GOSUB 2350
 2710 XX=X:YY=Y+1: GOSUB 2350
 2720 YY=Y-1: GOSUB 2350
 2730 IF J=0 THEN RETURN 
 2740 MNL(M)=X+DX+(Y+DY)*H:MAF=1
 2750 PRINT "WANDERING MONSTER!"
 2760 J=-1:K=0
 2770 P= RND (MFTTL)
 2780 FOR L=0 TO 4:K=K+MF(L): IF J>=0 THEN 2800
 2790 IF P<K THEN J=L
 2800 NEXT L: IF J<0 THEN J=4
 2804 IF J#4 THEN 2810: IF RND (100)>33 THEN 2810:J=5: IF RND (100)>50 THEN J=6
 2810 I=J
 2820 MAP(MNL(M))=MC(I)
 2830 MNT(M)=I
 2840 MNA(M)=CL+5:MRA=CL+5
 2850 MNS(M)=(PF/10)*(MS(I)/50)*DF
 2860 MND(M)=(PF/10)*(MD(I)/50)*DF
 2870 MNB(M)=(PF/10)*(MB(I)/50)*DF
 2880 C=MC(I):PX=X+DX:PY=Y+DY: CALL 2051
 2890 RETURN 
 2895 ALR=0:DIST=3
 2900 X1=PL MOD H:Y1=PL/H
 2910 FOR M=1 TO NR: IF MNL(M)<0 THEN 3050
 2920 X2=MNL(M) MOD H:Y2=MNL(M)/H
 2930 IF ABS (X2-X1)>(DIST) OR ABS (Y2-Y1)>(DIST) THEN 3050
 2940 MRA=CL+5:J=CL-MNA(M)+5:MNA(M)=MRA
 2950 IF MNT(M)#2 THEN 2990
 2960 MNB(M)=MNB(M)+(PF/20)*J
 2970 I=(PF/10)*(MB(MNT(M))/50)*DF
 2980 IF MNB(M)>I THEN MNB(M)=I
 2990 IF J<5 THEN 3050
 3000 IF J=0 THEN 3050
 3010 S=(PF/10)*(MS(MNT(M))/50)*DF:D=(PF/10)*(MD(MNT(M))/50)*DF
 3020 IF MNS(M)=S THEN 3030:MNS(M)=MNS(M)+(PF/20): IF MNS(M)>S THEN MNS(M)=S:J=J-1: IF J=0 THEN 3050
 3030 IF MND(M)=D THEN 3040:MND(M)=MND(M)+(PF/20): IF MND(M)>D THEN MND(M)=D:J=J-1: IF J=0 THEN 3050
 3040 IF MNS(M)<S OR MND(M)<D THEN 3000
 3050 NEXT M: RETURN 
 3060 MAF=0:X1=PL MOD H:Y1=PL/H
 3070 FOR M=1 TO NR: IF MNL(M)<0 THEN 3140
 3080 IF MNA(M)<=CL THEN 3140
 3090 X2=MNL(M) MOD H:Y2=MNL(M)/H
 3100 IF ABS (X2-X1)>1 OR ABS (Y2-Y1)>1 THEN 3130
 3110 IF ABS (X2-X1)#0 AND ABS (Y2-Y1)#0 THEN 3130
 3120 GOSUB 3150: GOTO 3140
 3130 GOSUB 3300
 3140 NEXT M: RETURN 
 3150 IF MNS(M)<(PF/20)*2 OR MND(M)=0 THEN RETURN 
 3160 ALR=1:OUT=1:UPN=1
 3165 CALL 4352
 3170 A=PA:D=MND(M):W=MW(MNT(M)): GOSUB 2520
 3180 NMS(M)=MNS(M)-(PF/20): PRINT NOM$(MNT(M)*8+1,MNT(M)*8+NOML(MNT(M)));" ATTACKS...";: GOSUB 2510
 3190 IF J#0 THEN 3210
 3200 PRINT "AND MISSES.": RETURN 
 3210 HITS=((W+1)*2*MNS(M))/20
 3215 IF MNS(M)>1000 THEN HITS=(W+1)*2*(MNS(M)/20)
 3220 IF HITS=0 THEN HITS=1
 3230 IF MNT(M)=1 THEN 3260
 3240 IF MNT(M)=0 THEN 3280
 3245 IF MNT(M)=6 THEN 3295
 3250 GOSUB 2410: RETURN 
 3260 PSM=PSM-HITS:PS=PS-HITS: PRINT "YOU LOSE ";HITS;" STR."
 3265 GOSUB 2090
 3270 IF PSM<0 THEN GOSUB 2440: IF PS<0 THEN PS=0: RETURN 
 3280 IF PA=0 OR PA>3 THEN 3250: IF RND (4) THEN 3250:PA=PA-1
 3290 PRINT "YOUR ARMOR IS DAMAGED.": RETURN 
 3295 PRINT "YOU FEEL A CHILL": GOSUB 2510:PS=PS-HITS: IF PS<0 THEN PS=0:PI=PI-HITS: IF PI<0 THEN PI=0:PD=PD-HITS: IF PD<0 THEN PD=0
 3296 GOSUB 2510: GOTO 3250
 3300 DX= SGN (X1-X2):DY= SGN (Y1-Y2)
 3310 XX=X2+DX:YY=Y2: IF MAP(XX+YY*H)=10 THEN 3340
 3320 XX=X2:YY=Y2+DY: IF MAP(XX+YY*H)=10 THEN 3340
 3330 RETURN 
 3340 Z=PF/100: IF MND(M)<Z THEN RETURN :MND(M)=MND(M)-Z
 3350 MAP(MNL(M))=10:MNL(M)=XX+YY*H:MAP(MNL(M))=MC(MNT(M))
 3360 PX=X2:PY=Y2: CALL 2054
 3370 IF C#MC(MNT(M)) THEN 3390:C=10
 3380 CALL 2051
 3390 IF ABS (X1-XX)>1 OR ABS (Y1-YY)>1 THEN RETURN :MAF=1:UPM=1
 3400 IF RND (3) THEN MNA(M)=MNA(M)+1: RETURN 
 3410 GOSUB 2090
 3420 GOSUB 2170
 3430 S=-16336:J=40
 3440 FOR I=1 TO J:X= PEEK (S)
 3445 NEXT I
 3446 IF DEAD=0 THEN 3450:DEAD=0: GOTO 3540
 3450 INP= PEEK (-16384): IF INP<128 THEN 3450
 3460 POKE -16368,0
 3470 CALL -936
 3480 R=3510: FOR I=1 TO LEN(COM$)
 3490 IF INP= ASC( COM$(I,I)) THEN R=RTN(I)
 3500 NEXT I: GOTO R
 3510 PRINT "WHAT? ENTER ONE OF THE FOLLOWING:"
 3520 PRINT COM$
 3530 GOTO 3430
 3540 UPN=1:UPM=1:BANK=BANK+GOLD:GOLD=BANK:BANK=0:PS=PSM:PI=PIM:PD=PDM:PB=PBM
 3550 GOSUB 2090:C=10:PX=PL MOD H:PY=PL/H: CALL 2051
 3560 PRINT "ENTER OPTION: TRADE,WEAPON,ARMOR,SCAN,": INPUT "DEEPER,RETURN - ",A$
 3570 IF LEN(A$)<1 THEN 3560
 3580 IF A$(1,1)="T" THEN GOSUB 3660
 3590 IF A$(1,1)="W" THEN GOSUB 3880
 3600 IF A$(1,1)="A" THEN GOSUB 3760
 3610 IF A$(1,1)="R" THEN 4120
 3620 IF A$(1,1)="D" THEN 4000
 3640 IF A$(1,1)="S" THEN GOSUB 4030
 3650 GOTO 3560
 3660 IF EXP<10 THEN RETURN : PRINT "EXPERIENCE MAY BE CONVERTED 10 FOR 1 FORANY ATTRIBUTE."
 3670 INPUT "HOW MUCH STRENGTH",X
 3680 GOSUB 2140:PSM=PSM+X:PS=PS+X: GOSUB 2090
 3690 IF EXP<10 THEN RETURN : INPUT "INTELLIGENCE",X
 3700 GOSUB 2140:PIM=PIM+X:PI=PI+X: GOSUB 2090
 3710 IF EXP<10 THEN RETURN : INPUT "DEXTERITY",X
 3720 GOSUB 2140:PDM=PDM+X:PD=PD+X: GOSUB 2090
 3730 IF EXP<10 THEN RETURN : INPUT "BODY",X
 3740 GOSUB 2140:PBM=PBM+X:PB=PB+X: GOSUB 2090
 3750 RETURN 
 3760 AC(1)=50:AC(2)=100:AC(3)=250
 3770 PRINT "1-LEATHER (50GP), 2-CHAIN (100GP),"
 3780 PRINT "3-PLATE (250GP)"
 3790 PRINT "YOU HAVE ";PA;". WHICH DO YOU WANT";: INPUT X
 3800 IF X<0 OR X>3 THEN 3770
 3810 IF X<=(PA) THEN 3870
 3820 IF AC(X)<=GOLD THEN 3840
 3830 GOSUB 3950: GOTO 3790
 3840 GOLD=GOLD-AC(X)
 3850 PA=X
 3860 PRINT "ITS YOURS..."
 3870 GOSUB 2090: GOSUB 2510: RETURN 
 3880 WC(1)=50:WC(2)=100
 3890 PRINT "1-HAND AXE (50GP), 2-SWORD (100GP)"
 3900 PRINT "YOU HAVE ";PW;". WHICH DO YOU WANT";: INPUT X
 3910 IF X<0 OR X>2 THEN 3890
 3920 IF X<=(PW) THEN 3990
 3930 IF WC(X)<=GOLD THEN 3960
 3940 GOSUB 3950: GOTO 3900
 3950 PRINT "YOU HAVEN'T ENOUGH GOLD": RETURN 
 3960 GOLD=GOLD-WC(X)
 3970 PW=X
 3980 PRINT "ITS YOURS..."
 3990 GOSUB 2090: GOSUB 2510: RETURN 
 4000 BANK=GOLD:GOLD=0:RD=40: GOSUB 2180:C=10:PX=PL MOD H:PY=PL/H: CALL 2051: CALL -936: GOTO 390
 4021 BANK=GOLD:GOLD=0:ERT=6610
 4023 CALL -936: PRINT "SAVING GAME...": INPUT "ASSIGN A NUMBER TO THIS GAME (0-15)",GN: IF GN<0 OR GN>15 THEN 4023
 4024 POKE 0,16+GN: CALL 994
 4028 GOLD=BANK:BANK=0: RETURN 
 4030 I=PSM/5+PIM/5+PDM/5+PBM/5+EXP/50
 4040 IF I>3000 THEN I=3000:I=I*10
 4050 PRINT "LAST SCAN: S";BSS;" I";BSI;" D";BSD;" B";BSB;" ";BSE;"EP"
 4060 PRINT "NEW SCAN COST-";I;"GP.";: INPUT " SCAN? ",A$
 4070 IF LEN(A$)=0 THEN RETURN : IF A$(1,1)#"Y" THEN RETURN 
 4080 IF GOLD>=I THEN 4100
 4090 PRINT "YOU CAN'T AFFORD IT.": GOSUB 2510: RETURN 
 4100 GOLD=GOLD-I:BSS=PSM:BSI=PIM:BSD=PDM:BSB=PBM:BSE=EXP
 4110 GOSUB 2090: PRINT "YOU HAVE BEEN SCANNED": GOSUB 2510: GOTO 4021
 4120 BANK=GOLD:GOLD=0
 4130 CALL -936:UPN=1
 4140 IF BANK=0 THEN 4170
 4150 PRINT "I WILL PUT THE REST OF YOUR GOLD IN THE"
 4160 PRINT "BANK FOR SAFE KEEPING."
 4170 IF MAP(PSL)=10 THEN 3410: GOSUB 2510: PRINT "STAIRCASE BLOCKED - TELEPORTING...": GOSUB 2510: GOSUB 2510: GOTO 6060
 4180 DX=0:DY=0
 4190 IF PD>0 THEN 4210
 4200 PRINT "YOUR DEXTERITY IS ZERO, BETTER REST": GOTO 3430
 4210 IF INP= ASC( "N") THEN DY=-1
 4220 IF INP= ASC( "S") THEN DY=1
 4230 IF INP= ASC( "E") THEN DX=1
 4240 IF INP= ASC( "W") THEN DX=-1
 4250 X=PL MOD H:Y=PL/H:XX=X+DX:YY=Y+DY
 4260 IF XX<0 OR XX>=H OR YY<0 OR YY>=V THEN 4420
 4270 IF MAP(XX+YY*H)#10 THEN 4420
 4280 J=PS-(GOLD/10)-PA*2: IF J>=0 THEN NCL=1: IF J<0 THEN NCL=2: IF J>=-PS THEN 4310
 4290 IF GOLD=0 THEN 4310
 4300 PRINT "YOU ARE TOO LOADED DOWN TO MOVE": GOTO 3430
 4310 NCL=(NCL*PF)/PD: IF NCL>0 THEN 4320:NCL=1
 4320 SC=SC+1: IF SC<5 THEN 4330:PD=PD-(PF/20):SC=0
 4330 PL=XX+YY*H:UPM=1:UPN=1
 4340 IF RND (10)#9 OR MI(8)#0 THEN 4350:ALR=1
 4350 IF PL#PSL THEN 4410
 4360 PRINT "YOU ARE AT THE MAIN STAIRCASE."
 4370 INPUT "DO YOU WANT TO EXIT TO SPEND EXP/GOLD?",A$
 4380 GOSUB 2170
 4390 IF LEN(A$)<1 THEN 4410
 4400 IF A$(1,1)="Y" THEN 3540
 4405 IF A$(1,1)#"N" THEN 4360
 4410 IF NCL=0 THEN 3410:NCL=NCL-1: GOSUB 2570: IF OUT=0 THEN 4410: GOTO 3410
 4420 GOSUB 2570: GOTO 3410
 4430 CLR=8:X=PL MOD H:Y=PL/H: GOSUB 2210
 4440 IF J#0 THEN 4470
 4450 PRINT "I SEE NO DOOR"
 4460 GOTO 3430
 4470 P= RND (100):ALR=1
 4475 CALL 4096
 4480 IF INP= ASC( "K") THEN P=P*2
 4490 IF MI(5)=1 THEN 4540
 4500 IF (P/10)<=(6*PS)/PF THEN 4540
 4510 PS=PS-(PF/20): IF PS<0 THEN PS=0:UPN=1
 4520 PRINT "WHAM!!! THE DOOR DOESN'T BUDGE"
 4530 IF INP= ASC( "O") THEN 3410: GOSUB 2570: GOTO 3410
 4540 PS=PS-(PF/20): IF PS<0 THEN PS=0:UPN=1
 4550 PRINT "CRASH!! THE DOOR GIVES WAY"
 4560 PL=X+DX+(Y+DY)*H:UPM=1
 4570 IF INP# ASC( "B") THEN 4530
 4580 XD=DX:YD=DY: GOSUB 2170:DX=XD:DY=YD
 4590 GOSUB 2090: GOTO 4250
 4600 CLR=255:X=PL MOD H:Y=PL/H: GOSUB 2210
 4610 IF J#0 THEN 4630
 4620 PRINT "I SEE NO MONSTER": GOSUB 2570: GOTO 3410
 4630 X=X+DX:Y=Y+DY
 4640 M=1
 4641 IF (X+Y*H)=MNL(M) THEN 4650:M=M+1: IF M<=NR THEN 4641: GOTO 4620
 4650 IF INP# ASC( "Z") THEN 4670
 4660 W=3:DMG= RND (PIM/2): IF MI(7)=0 THEN PI=PI-DMG: GOTO 4680
 4670 IF PS<PF/20 THEN 4860:W=PW:PS=PS-(PF/20):DMG=((W+1)*2*PS)/20: IF PS>1000 THEN DMG=(W+1)*2*(PS/20)
 4680 CALL 4352
 4685 UPN=1:A=MA(MNT(M)):D=PD: GOSUB 2520
 4690 ALR=1
 4700 IF J#0 THEN 4720
 4710 PRINT "YOU MISSED": GOTO 4850
 4720 IF MNT(M)#1 AND MNT(M)#10 THEN 4750
 4730 IF INP= ASC( "Z") OR PW>2 THEN 4750
 4740 PRINT "ONLY MAGICAL WEAPONS AFFECT UNDEAD": GOTO 4850
 4750 IF DMG=0 THEN DMG=1:MNB(M)=MNB(M)-DMG
 4760 GOTO RND (3)*10+4770
 4770 PRINT "AAARG!!!";: GOTO 4800
 4780 PRINT "OUCH!!";: GOTO 4800
 4790 PRINT "YOWLP!!";
 4800 PRINT " YOU INFLICT ";DMG;" HITS": IF MNB(M)>0 THEN 4850
 4810 MAP(MNL(M))=10:MNL(M)=-1
 4820 PRINT "THE MONSTER IS DEAD"
 4830 UPM=1
 4840 EXP=EXP+(PF/10)*(MB(MNT(M))/50)*DF
 4850 IF PEEK (4352)#96 THEN GOSUB 2510: GOSUB 2570: GOTO 3410
 4860 PRINT "YOU ARE TOO WEAK TO FIGHT": GOTO 3430
 4870 PRINT "SHHHH..."
 4880 CLR=8:X=PL MOD H:Y=PL/H: GOSUB 2210: IF J#0 THEN 4890:DX=0:DY=0
 4890 J=0:X=(PL MOD H)+DX:Y=(PL/H)+DY
 4900 FOR DX=1 TO -1 STEP -1
 4910 FOR DY=1 TO -1 STEP -1
 4920 XX=X+DX:YY=Y+DY
 4930 IF ABS (DX)= ABS (DY) THEN 5020
 4940 IF XX<0 OR XX>=H OR YY<0 OR YY>=V THEN 5020
 4950 II=0
 4951 IF MAP(XX+YY*H)=MC(II) THEN 4960:II=II+1: IF II<=NMN THEN 4951: GOTO 5020
 4960 IF II>4 THEN 5020: GOTO 4970+II*10
 4970 PRINT "YOU HEAR OOZING": GOSUB 2510:J=J+1: GOTO 5020
 4980 GOTO 5020
 4990 PRINT "YOU HEAR 'TROLLISH'": GOSUB 2510:J=J+1: GOTO 5020
 5000 PRINT "YOU HEAR SLITHERING": GOSUB 2510:J=J+1: GOTO 5020
 5010 PRINT "YOU HEAR HISSING": GOSUB 2510:J=J+1
 5020 NEXT DY,DX
 5030 IF J=0 THEN PRINT "YOU HEAR NOTHING"
 5040 GOTO 3410
 5050 GOSUB 2570: IF OUT=1 THEN 3410: GOSUB 2570: IF OUT=1 THEN 3410: GOSUB 2570: IF OUT=1 THEN 3410:J=0
 5060 X=PL MOD H:Y=PL/H
 5070 FOR I=1 TO NSD
 5080 XX=(SDL(I) MOD H)-X:YY=(SDL(I)/H)-Y
 5090 IF (XX<-1 OR XX>1) OR (YY<-1 OR YY>1) THEN 5110
 5100 MAP(SDL(I))=8:J=J+1
 5110 NEXT I
 5120 IF J=0 THEN PRINT "NO SECRET DOORS"
 5130 IF J#0 THEN UPM=1
 5140 GOTO 3410
 5150 CLR=13:X=PL MOD H:Y=PL/H: GOSUB 2210
 5160 IF J#0 THEN 5200
 5170 CLR=8: GOSUB 2210: IF J=0 THEN 5180: IF MI(5) THEN 4560: PRINT "ITS LOCKED": GOTO 3410
 5180 PRINT "I SEE NO CHEST"
 5190 GOTO 3430
 5200 X=X+DX:Y=Y+DY
 5210 CH=1
 5220 IF (X+Y*H)=CHL(CH) THEN 5240
 5230 CH=CH+1: IF CH<=NR THEN 5220: GOTO 5180
 5240 IF CHS(CH)=0 THEN 5430
 5250 PRINT "THE CHEST IS LOCKED"
 5260 INPUT "BREAK THE LOCK?",A$
 5270 IF LEN(A$)=0 THEN 5250: IF A$(1,1)="Y" THEN 5280: PRINT : PRINT : GOTO 3430
 5280 ALR=1
 5285 CALL 4352
 5290 GOTO 5300+(CHS(CH)-1)*20
 5300 PRINT "WHEW! NO TRAPS": GOSUB 2510
 5310 GOTO 5420
 5320 PRINT "POISON GAS!": GOSUB 2510: GOSUB 2510
 5330 IF RND (10)<(PD*5)/PF THEN 5410: GOTO 5400
 5340 PRINT "POISON DART!": GOSUB 2510: GOSUB 2510
 5350 IF RND (10)<(PI*5)/PF THEN 5410: GOTO 5400
 5360 PRINT "ACID!": GOSUB 2510: GOSUB 2510
 5370 IF RND (10)<(PD*5)/PF THEN 5410
 5400 HITS= RND (PF/2)+1: GOSUB 2410: GOTO 5420
 5410 PRINT "IT MISSED YOU"
 5420 CHS(CH)=0: GOSUB 2510
 5430 IF CHC(CH)<0 THEN 5490
 5440 IF CHC(CH)>0 THEN 5460
 5450 PRINT "THE CHEST IS EMPTY": GOTO 5480
 5460 GOLD=GOLD+CHC(CH):EXP=EXP+CHC(CH)
 5470 PRINT "THE CHEST CONTAINED ";CHC(CH);"GP"
 5480 CHC(CH)=0:UPN=1: GOSUB 2510: GOSUB 2570: GOTO 3410
 5490 PRINT "YOU FIND ";
 5500 IF CHC(CH)=-99 THEN PRINT "A GOLDEN APPLE. ";
 5510 IF CHC(CH)=-1 THEN PRINT "A SWORD. ";
 5520 IF CHC(CH)=-2 THEN PRINT "ARMOR. ";
 5530 IF (CHC(CH)>-5 AND CHC(CH)<-2) OR (CHC(CH)>-11 AND CHC(CH)<-8) THEN PRINT "A POTION. ";
 5540 IF CHC(CH)<-4 AND CHC(CH)>-8 THEN PRINT "A WAND ";
 5542 IF CHC(CH)=-8 THEN PRINT "BOOTS"
 5550 INPUT "DO YOU TAKE IT?",A$
 5560 IF A$(1,1)="Y" THEN GOTO 5590
 5570 IF A$(1,1)#"N" THEN 5550
 5580 PRINT "POOF! ITS GONE": GOTO 5480
 5590 X=-CHC(CH): IF X=99 THEN 6200:MI(X)=1:CHC(CH)=0
 5600 GOTO 5600+X*10
 5610 PW=3
 5615 PRINT "ITS MAGIC!": GOTO 5480
 5620 PA=4: GOTO 5615
 5630 PRINT "YOUR MAXIMUM STRENGTH IS DOUBLED!"
 5635 PS=PS+PSM:PSM=PSM*2: GOTO 5480
 5640 PRINT "YOU LOST YOUR MEMORY"
 5641 UPM=1: CALL 2048
 5642 GOTO 5480: GOTO 5480
 5650 PRINT "IT UNLOCKS DOORS": GOTO 5480
 5660 PRINT "YOUR MAX INTELLIGENCE IS HALVED"
 5665 PIM=PIM/2: IF PI>PIM THEN PI=PIM: GOTO 5480
 5670 PRINT "ITS A ZAP WAND": GOTO 5480
 5680 PRINT "NOW YOU ARE SILENT!": GOTO 5480
 5690 PRINT "IT'S POISON!":HITS=PB: GOSUB 2420: GOTO 5480
 5700 PRINT "YOU ARE CLAIRVOYANT!":UPM=1: GOTO 5480
 5710 IF PD<0 THEN 4200: PRINT "HELP! HELP! HELP!"
 5720 G=GOLD-10*(PS-PA*2): IF G<=0 OR GOLD<=0 THEN 5760: PRINT "YOU HAD TO DROP SOME GOLD": GOSUB 5730: GOTO 5760
 5730 IF G=0 THEN RETURN : IF G>GOLD THEN G=GOLD: FOR J=1 TO NR: IF CHL(J)<0 THEN 5750: IF CHC(J)<0 THEN 5750
 5740 CHC(J)=CHC(J)+G:EXP=EXP-G:GOLD=GOLD-G:G=0
 5750 NEXT J: RETURN 
 5760 K=10:PD=PD/2
 5770 CLR=10:X=PL MOD H:Y=PL/H: GOSUB 2210: IF J#0 THEN 5780: PRINT "THERE IS NOWHERE TO RUN!": GOTO 3410
 5780 K=K-1: IF X+DX<0 OR X+DX>=H OR Y+DY<0 OR Y+DY>=V THEN 5850
 5790 IF K=0 THEN 5850:L=PL+DX+DY*H
 5800 IF MAP(L)#10 THEN 5850
 5810 C=MAP(PL):PX=X:PY=Y: CALL 2051
 5820 PL=L:X=PL MOD H:Y=PL/H:C=2
 5830 PX=X:PY=Y: CALL 2051
 5840 GOTO 5780
 5850 UPM=1:UPN=1:ALR=1
 5852 CALL 4096
 5853 GOSUB 2570: GOTO 3410
 5860 NCL=INP-176
 5870 IF NCL=0 THEN NCL=32767
 5880 SC=0
 5890 IF PS=PSM THEN 5910
 5900 PS=PS+(PF/20): IF PS>PSM THEN PS=PSM:NCL=NCL-1: GOSUB 2570: IF OUT=1 THEN 3410: IF NCL=0 THEN 5960
 5910 IF PI=PIM THEN 5930
 5920 PI=PI+(PF/20): IF PI>PIM THEN PI=PIM:NCL=NCL-1: GOSUB 2570: IF OUT=1 THEN 3410: IF NCL=0 THEN 5960
 5930 IF PD=PDM THEN 5950
 5940 PD=PD+(PF/20): IF PD>PDM THEN PD=PDM:NCL=NCL-1: GOSUB 2570: IF OUT=1 THEN 3410: IF NCL=0 THEN 5960
 5950 IF PS#PSM OR PI#PIM OR PD#PDM THEN 5890
 5960 UPN=1: IF INP= ASC( "0") THEN 3410: IF NCL<=0 THEN 3410:N=1
 5961 GOSUB 2570: IF OUT=1 THEN 3410:N=N+1: IF N<NCL THEN 5961: GOTO 3410
 5970 IF PI<PF/10 THEN 6130
 5980 IF PB<PBM THEN 6000
 5990 PRINT "YOU ARE ALREADY HEALED": GOTO 3410
 6000 PI=PI-(PF/10):NCL=1
 6010 PB=PB+(PF/10): IF PB>PBM THEN PB=PBM:UPN=1: GOSUB 2570: GOTO 3410
 6020 IF PI<(PF/4)+5 THEN 6130
 6030 PI=PI-(PF/4)-5
 6040 G=GOLD: GOSUB 5730
 6050 C=MAP(PL):PX=PL MOD H:PY=PL/H: CALL 2051
 6060 PL= RND (H*V)
 6070 IF PL>=H*V THEN PL=0
 6080 IF MAP(PL)=10 THEN 6100
 6090 GOTO 6060
 6100 ALR=1: PRINT "POOF!"
 6105 CALL 4352
 6110 GOSUB 2090: GOSUB 2170: GOSUB 2570: GOTO 3410
 6120 IF PI>=(PF/4)+15 THEN 6150
 6130 PRINT "YOUR INTELLIGENCE IS TOO LOW": GOTO 3430
 6140 GOSUB 2570: IF OUT=1 THEN 3410: GOSUB 2570: IF OUT=1 THEN 3410: GOSUB 2570: IF OUT=1 THEN 3410
 6150 PI=PI-(PF/4)-15:UPN=1
 6160 RD=3: GOSUB 2180: PRINT "XRAY COMPLETE": GOTO 3410
 6170 IF PI<PIM/2 AND MI(7)=0 THEN 6130
 6180 GOTO 4600
 6190 G=GOLD-20*(PS-PA*2): IF G<=0 THEN 3410: GOSUB 5730:UPN=1: GOTO 3410
 6200 IF RND (2) THEN 6240: PRINT "BOOM! IT EXPLODES!"
 6210 CALL 4096
 6215 GOSUB 2510: GOSUB 2510
 6220 PRINT "MUST NOT BE THE ONE YOU'RE LOOKING FOR"
 6225 GOSUB 2510: GOSUB 2510
 6230 PB=0: GOSUB 2440: GOTO 5480
 6240 TEXT : CALL -936: PRINT "CONGRATULATIONS! YOU WIN!!!"
 6245 VTAB 5
 6250 PRINT : PRINT "YOU'VE FOUND THE MOST POWERFUL MAGICAL": PRINT "DEVICE OF THEM ALL --": PRINT 
 6255 PRINT "THE FABLED GOLDEN APPLE OF APPLE MANOR!": PRINT 
 6256 VTAB 15
 6260 PRINT : PRINT "PRESS RETURN TO SEE WHAT IT CAN DO...": INPUT A$: CALL 979
 6300 PRINT "YOU HAVE...": GOSUB 2510
 6310 GOSUB 2510: PRINT 
 6320 GOTO 6330+10*PW
 6330 PRINT "  DAGGER": GOTO 6380
 6340 PRINT "  HAND AXE": GOTO 6380
 6350 PRINT "  SWORD": GOTO 6380
 6360 PRINT "  MAGIC SWORD"
 6380 GOSUB 2510: GOSUB 2510
 6385 PRINT 
 6390 GOTO 6400+10*PA
 6400 PRINT "  NO ARMOR": GOTO 6460
 6410 PRINT "  LEATHER ARMOR": GOTO 6460
 6420 PRINT "  CHAIN MAIL": GOTO 6460
 6430 PRINT "  PLATE ARMOR": GOTO 6460
 6440 PRINT "  MAGIC ARMOR"
 6460 GOSUB 2510: GOSUB 2510
 6465 PRINT 
 6480 IF MI(5)=0 THEN 6490: PRINT : PRINT "  DOOR UNLOCKING WAND": GOSUB 2510: GOSUB 2510
 6490 IF MI(7)=0 THEN 6500: PRINT : PRINT "  ZAP WAND": GOSUB 2510: GOSUB 2510
 6500 IF MI(8)=0 THEN 6510: PRINT : PRINT "  BOOTS OF SILENCE": GOSUB 2510: GOSUB 2510
 6510 IF MI(10)=0 THEN 6520: PRINT : PRINT "  CLAIRVOYANCE"
 6520 GOTO 3410
 6600 PRINT : PRINT "GAME NOT FOUND OR DAMAGED": POKE 223,0: GOTO 222
 6610 PRINT : PRINT "DOS ERROR": POKE 223,0: GOTO 3540
