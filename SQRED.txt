 5 P% = 0:L% = 0
 10 XM = 14:YM = 14
 20  DIM A%(XM,YM)
 40  FOR X = 1 TO XM: FOR Y = 1 TO YM
 50 A%(X,Y) = 0: NEXT Y,X
 60  HGR2 : POKE  - 16302,0
 71  GOTO 100
 75  PRINT X;",";Y;",";Z: RETURN 
 76  INPUT X,Y,Z: RETURN 
 80  INVERSE : RETURN 
 81  VTAB Y: FOR X = 1 TO Z: HTAB X: PRINT A%(X,Y);: NEXT X
 82  FOR X = XM - Z + 1 TO XM: HTAB X: PRINT A%(X,Y);: NEXT X
 83  VTAB YM - Y + 1: FOR X = 1 TO Z: HTAB X: PRINT A%(X,Y);: NEXT X
 84  FOR X = XM - Z + 1 TO XM: HTAB X: PRINT A%(X,Y);: NEXT X
 85 Y = Y + 1: RETURN 
 100  TEXT 
 120  CALL  - 936
 140  FOR Y = 1 TO YM: FOR X = 1 TO XM: PRINT A%(X,Y);: NEXT X
 141  PRINT " ";: GOSUB 8000
 142  IF Y <  > 24 THEN  PRINT 
 143  NEXT Y
 144  PRINT 
 145  PRINT "30303030303030 MAGENTA"
 146  PRINT "03030303030303 GREEN"
 147  PRINT "74747474747474 BLUE"
 148  PRINT "47474747474747 ORANGE"
 170  VTAB 1: HTAB 30: INVERSE : PRINT "SQRED     ": NORMAL : VTAB 3: HTAB 30: PRINT "U=UP"
 171  HTAB 30: PRINT "D=DOWN  "
 172  HTAB 30: PRINT "L=LEFT"
 173  HTAB 30: PRINT "R=RIGHT"
 174  HTAB 30: PRINT 
 175  HTAB 30: PRINT "P=PLOT": HTAB 30: PRINT "S=SET ALL"
 176  HTAB 30: PRINT "X=INVERT": HTAB 30: PRINT "E=END"
 177  HTAB 30: PRINT "I=INPUT"
 178  HTAB 30: PRINT "O=OUTPUT"
 180  HTAB 30: PRINT 
 181  HTAB 30: PRINT "0=BLACK 1"
 182  HTAB 30: PRINT "1=GREEN"
 183  HTAB 30: PRINT "2=MAGENTA"
 184  HTAB 30: PRINT "3=WHITE 1"
 185  HTAB 30: PRINT "4=BLACK 2"
 186  HTAB 30: PRINT "5=ORANGE"
 187  HTAB 30: PRINT "6=BLUE"
 188  HTAB 30: PRINT "7=WHITE 2"
 200 X = (XM + 1) / 2:Y = (YM + 1) / 2
 210  VTAB Y: HTAB X
 220  FLASH : PRINT A%(X,Y);: NORMAL 
 240 C =  PEEK ( - 16384): IF C < 128 THEN 240
 260  POKE  - 16368,0
 265  VTAB Y: HTAB X: NORMAL : PRINT A%(X,Y);
 280 C = C - 128
 285  IF C =  ASC (" ") THEN C = A%(1,Y) + 48: GOTO 295
 290  IF C <  ASC ("0") OR C >  ASC ("7") THEN 300
 295 A%(X,Y) = C - 48
 296  VTAB Y: HTAB X: NORMAL : PRINT A%(X,Y);:X = X + 1
 300  IF C =  ASC ("U") THEN Y = Y - 1
 320  IF C =  ASC ("D") THEN Y = Y + 1
 340  IF C =  ASC ("R") THEN X = X + 1
 360  IF C =  ASC ("L") THEN X = X - 1
 361  IF C =  ASC ("S") THEN 5000
 362  IF C =  ASC ("X") THEN 6000
 365  IF C =  ASC ("O") THEN 2000
 370  IF C =  ASC ("I") THEN 3000
 380  IF Y > YM THEN Y = 1
 382  IF Y < 1 THEN Y = YM
 400  IF X > XM THEN X = 1
 402  IF X < 1 THEN X = XM
 420  IF C =  ASC ("E") THEN 9999
 425  IF C =  ASC ("P") THEN 1000
 440  GOTO 210
 1000  POKE  - 16299,0: POKE  - 16304,0: POKE  - 16297,0: POKE  - 16302,0
 1010 X1 =  - 1:Y1 =  - 1: GOSUB 7000
 1020  FOR Y = 1 TO YM
 1040  FOR X = 1 TO XM
 1060  HCOLOR= A%(X,Y)
 1080  HPLOT X + X1,Y + Y1
 1140  NEXT X,Y
 1145  GOTO 1000
 1160 C =  PEEK ( - 16384): IF C < 128 THEN 1160
 1180  POKE  - 16368,0
 1200  GOTO 100
 2000  TEXT : CALL  - 936
 2010  PRINT "NAME OF FIGURE TO SAVE?": INPUT F$
 2012  IF F$ = "NONE" THEN 100
 2015  IF F$ = "" THEN  PRINT "CATALOG": GOTO 2010
 2020 P% =  PEEK (107) + 256 *  PEEK (108)
 2030 L% =  PEEK (P% + 2) + (256 *  PEEK (P% + 3))
 2040  PRINT "BSAVE ";F$;",A";P%;",L";L%
 2045  INPUT "WRITE DFBS?";A$: IF A$ <  > "Y" THEN 100
 2050 D$ = ""
 2055  PRINT D$;"OPEN DFBS": PRINT D$;"CLOSE DFBS"
 2060  PRINT D$;"APPEND DFBS"
 2070  PRINT D$;"WRITE DFBS"
 2080 Y = 1: GOTO 2106
 2090  IF Y > YM THEN 2999
 2100  IF J <  > 4 THEN 2110
 2105  PRINT " ";F$
 2106 J = 1: PRINT " DFB ";
 2110  IF J <  > 1 THEN  PRINT ",";
 2120  GOSUB 8000:Y = Y + 1:J = J + 1
 2130  GOTO 2090
 2999  PRINT : PRINT D$;"CLOSE DFBS": GOTO 100
 3000  TEXT : CALL  - 936
 3010  PRINT "NAME OF FIGURE TO LOAD?": INPUT F$
 3015  IF F$ = "" THEN  PRINT "CATALOG": GOTO 3010
 3020 P% =  PEEK (107) + 256 *  PEEK (108)
 3030  PRINT "BLOAD ";F$;",A";P%
 3120  GOTO 100
 5000 C =  PEEK ( - 16384): IF C < 128 THEN 5000
 5010  POKE  - 16368,0
 5020 C = C - 128
 5030  IF C <  ASC ("0") OR C >  ASC ("7") THEN 100
 5040 C = C - 48
 5050  FOR X = 1 TO XM: FOR Y = 1 TO YM:A%(X,Y) = C: NEXT Y,X: GOTO 100
 6000  FOR Y = 1 TO YM
 6010  FOR X = 1 TO XM
 6020 A%(X,Y) = 7 - A%(X,Y)
 6030  NEXT X,Y
 6040  GOTO 100
 7000 X = ( PDL (0) / 240) * 20
 7010  IF X > 19 THEN X = 19
 7012 Y = ( PDL (1) / 240) * 13
 7014  IF Y > 12 THEN Y = 12
 7015 X =  INT (X):Y =  INT (Y)
 7020  HCOLOR= 0: HPLOT X * 14,Y * 14
 7030  HCOLOR= 3: HPLOT X * 14,Y * 14
 7035  IF  PEEK ( - 16384) < 128 THEN 7040
 7036  POP : GOTO 1180
 7040  IF  PEEK ( - 16287) < 128 THEN 7000
 7050 X1 = X * 14 - 1:Y1 = Y * 14 - 1: RETURN 
 8000 HX = 1: PRINT "$";
 8010 HV = (A%(HX,Y) <  > 0) + 2 * (A%(HX + 1,Y) <  > 0) + 4 * (A%(HX + 2,Y) <  > 0) + 8 * (A%(HX + 3,Y) <  > 0) + 16 * (A%(HX + 4,Y) <  > 0) + 32 * (A%(HX + 5,Y) <  > 0) + 64 * (A%(HX + 6,Y) <  > 0)
 8015 HV = HV - (A%(HX,Y) = 4) - 2 * (A%(HX + 1,Y) = 4) - 4 * (A%(HX + 2,Y) = 4) - 8 * (A%(HX + 3,Y) = 4) - 16 * (A%(HX + 4,Y) = 4) - 32 * (A%(HX + 5,Y) = 4) - 64 * (A%(HX + 6,Y) = 4)
 8016 HV = HV + (A%(HX,Y) > 3) * 128
 8020  POKE 768,169: POKE 769,HV: POKE 770,76: POKE 771,218: POKE 772,253: CALL 768
 8030  IF HX <  > 1 THEN  RETURN 
 8040  PRINT ",$";:HX = 8: GOTO 8010
 9999  CALL  - 936: END 
